#!/bin/tcsh

# load configuration for pdqBLAST
if ( ! -r ~/.pdqBLASTrc ) then
	echo "ERROR: unable to find or read configuration file for pdqBLAST (expected as ~/.pdqBLASTrc)" > /dev/stderr
	exit 1
endif
source ~/.pdqBLASTrc

if ($#argv != 2) then
	echo "usage: pdqBLAST_job_status <job_id> <work_unit_number>" > /dev/stderr
	exit 1
endif

set job_id = $argv[1]
set work_unit_number = $argv[2]

set node = `echo "CALL job_work_unit_node($job_id, $work_unit_number, @node); SELECT @node;" | mysql -h $PDQBLAST_MYSQL_HOST pdqBLAST -s`

if ( "$node" == "NULL" ) then
	echo "ERROR: unable to find node for job $job_id and work unit number $work_unit_number" > /dev/stderr
	exit 1
endif

set node_temporary_directory = `echo "CALL job_work_unit_temporary_directory($job_id, $work_unit_number, @temporary_directory); SELECT @temporary_directory;" | mysql -h $PDQBLAST_MYSQL_HOST pdqBLAST -s`

if ( "$node_temporary_directory" == "NULL" ) then
	echo "ERROR: unable to find node temporary directory for job $job_id and work unit number $work_unit_number" > /dev/stderr
	exit 1
endif

echo "running command:"
echo "ssh $node '\\rm -rf $node_temporary_directory'"
ssh $node "\rm -rf $node_temporary_directory"
